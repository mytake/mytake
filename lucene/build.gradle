apply plugin: 'java'

String VER_LUCENE = '8.6.2'
dependencies {
	implementation project(':client-interface')
	implementation 'com.diffplug.durian:durian-core:1.2.0'
	implementation 'com.diffplug.durian:durian-collect:1.2.0'
	implementation 'com.diffplug.durian:durian-io:1.2.0'
	implementation "org.apache.lucene:lucene-core:${VER_LUCENE}"
	implementation "org.apache.lucene:lucene-queryparser:${VER_LUCENE}"
	implementation "org.apache.lucene:lucene-analyzers-common:${VER_LUCENE}"
	testImplementation project(':factset-tooling')
	testImplementation "com.squareup.okhttp3:okhttp:${VER_OKHTTP}"
	testImplementation "com.google.code.gson:gson:${VER_GSON}"
	testImplementation "junit:junit:${VER_JUNIT}"
	testImplementation "org.assertj:assertj-core:${VER_ASSERTJ}"
}

apply plugin: 'com.diffplug.spotless'
spotless {
	ratchetFrom 'origin/staging'
	java {
		licenseHeaderFile rootProject.file('gradle/spotless-license-agpl.java')
		importOrder()
		eclipse().configFile(rootProject.file('gradle/spotless.eclipseformat.xml'))
		removeUnusedImports()
	}
}

String debatesShaOrTag = 'a68e1678482f113854dd0f7f82ef50ff142bc04b'
tasks.register('generateIndex', JavaExec) {
	description = 'Generates the foundation'
	classpath = sourceSets.test.runtimeClasspath
	main = 'org.mytake.factset.search.GenerateIndex'
	args = [
		debatesShaOrTag
	]

	dependsOn 'testClasses'
	inputs.property('us-pres-debates', debatesShaOrTag)
	inputs.files(sourceSets.test.runtimeClasspath).withPathSensitivity(PathSensitivity.NONE)

	outputs.dir('build/search-index')
	outputs.cacheIf { true }
}
